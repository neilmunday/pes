# Based on https://archlinuxarm.org/packages/armv7h/qt5-quickcontrols/files/PKGBUILD

pkgname=pes-qt5-quickcontrols
_qtver=5.9.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('armv7h')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QtQuickControls'
depends=('pes-qt5-declarative')
makedepends=('python2' 'cmake')
groups=('pes')
_pkgfqn="qtquickcontrols-opensource-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('520ed0e110f9d0977eb873ea0e05a3d205c9c1b1ad2dd606875708848b21ec2e')

prepare() {
  mkdir -p build

  # Use python2 for Python 2.x
  find -name '*.pro' -o -name '*.pri' | xargs sed -i -e 's|python -c|python2 -c|g' -e 's|python \$|python2 \$|g'
}

build() {
  cd build

  export PATH=/opt/pes/bin:$PATH
  export LD_LIBRARY_PATH=/opt/pes/lib

  export PYTHON=python2
  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install
}
