# Based on https://archlinuxarm.org/packages/armv7h/qt5-quickcontrols2/files/PKGBUILD

pkgname=pes-qt5-quickcontrols2
_qtver=5.9.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('armv7h')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QtQuickControls2'
depends=('pes-qt5-declarative')
makedepends=('python2' 'cmake')
groups=('pes')
_pkgfqn="qtquickcontrols2-opensource-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('203be0321112d1513b722fce463f9e30d7d00befb33349901d9517f4f13408ff')

prepare() {
  mkdir -p build

  # Use python2 for Python 2.x
  find -name '*.pro' -o -name '*.pri' | xargs sed -i -e 's|python -c|python2 -c|g' -e 's|python \$|python2 \$|g'
}

build() {
  cd build

  export PATH=/opt/pes/bin:$PATH
  export LD_LIBRARY_PATH=/opt/pes/lib

  export PYTHON=python2
  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/opt/pes/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

}
